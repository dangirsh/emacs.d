#+TITLE: Emacs config
#+AUTHOR: Pierre Lecocq
#+EMAIL: pierre.lecocq@gmail.com
#+STARTUP: content

This is yet another stop to the biggest lie in the world: a perfect Emacs configuration file.

Note: the =emacs.el= configuration file is generated from the =README.org= file using [[http://orgmode.org/][org-mode]] and [[https://en.wikipedia.org/wiki/Literate_programming][literate programming]].

* Usage

In order to use this configuration, follow these three easy steps:

- Clone the repository

#+begin_src sh
mkdir -p ~/work/src
git clone https://github.com/pierre-lecocq/emacs.d ~/work/src/emacs.d
cd ~/work/src/emacs.d
#+end_src

- Use the makefile to build the configuration file from the README

#+begin_src sh
make
#+end_src

- Load the configuration file

#+begin_src sh
echo "(load \"~/work/src/emacs.d/emacs\")" > ~/.emacs
#+end_src

* Initialization

** Internals

Set the debug flag. Because we can not tolerate any error from the init file

#+begin_src emacs-lisp :tangle emacs.el
(setq debug-on-error t)
#+end_src

Set the current file directory as the main config directory

#+begin_src emacs-lisp :tangle emacs.el
(setq config-dir "~/work/src/emacs.d/")
#+end_src

Set some internal Emacs variables

#+begin_src emacs-lisp :tangle emacs.el
(setq gc-cons-threshold 100000000
      ;; Identity
      user-full-name "Pierre Lecocq"
      user-mail-address "pierre.lecocq@gmail.com"
      ;; Formats
      frame-title-format "Emacs %f"
      time-stamp-format "%:y-%02m-%02d %02H:%02M:%02S"
      ;; Scratch and Splash
      initial-scratch-message (format ";; Scratch buffer - started on %s\n\n" (current-time-string))
      inhibit-startup-message t
      inhibit-splash-screen t
      ;; General behaviour
      kill-whole-line t
      require-final-newline t
      next-line-add-newlines nil
      ;; Backup
      backup-inhibited t
      make-backup-files nil
      auto-save-default nil
      ;; VC
      vc-follow-symlinks t
      ;; Recentf
      recentf-auto-cleanup 'never
      recentf-max-menu-items 50
      recentf-keep '(file-remote-p file-readable-p)
      ;; Password cache
      password-cache-expiry nil
      ;; Uniquify
      uniquify-buffer-name-style 'forward uniquify-separator "/"
      ;; Paths
      packages-dir (expand-file-name (concat config-dir "packages"))
      host-file (expand-file-name (concat config-dir (format "host-%s.el" (downcase (car (split-string (system-name) "\\.")))))))
#+end_src

** Libraries and modes

*** Require some internal libraries

#+begin_src emacs-lisp :tangle emacs.el
(mapc #'require '(autoinsert
                  bookmark
                  em-alias
                  linum
                  paren
                  recentf
                  time-stamp
                  whitespace))
#+end_src

*** Turn on some internal modes

#+begin_src emacs-lisp :tangle emacs.el
(mapc (lambda (mode)
        (when (fboundp mode)
          (funcall mode 1)))
      '(auto-compression-mode
        auto-insert-mode
        column-number-mode
        global-auto-revert-mode
        global-font-lock-mode
        global-hl-line-mode
        line-number-mode
        recentf-mode
        show-paren-mode
        transient-mark-mode
        which-function-mode))
#+end_src

*** Turn off some internal modes

#+begin_src emacs-lisp :tangle emacs.el
(mapc (lambda (mode)
        (when (fboundp mode)
          (funcall mode -1)))
      '(menu-bar-mode
        tool-bar-mode
        scroll-bar-mode))
#+end_src

** General behaviour

*** Laziness. First.

Because "yes" is way too long to type

#+begin_src emacs-lisp :tangle emacs.el
(fset 'yes-or-no-p 'y-or-n-p)
#+end_src

*** Indentation

Set indentation

#+begin_src emacs-lisp :tangle emacs.el
(setq-default tab-width 4
              c-basic-offset 4
              c-hanging-comment-ender-p nil
              indent-tabs-mode nil)
#+end_src

*** Locale

Set locale

#+begin_src emacs-lisp :tangle emacs.el
(set-language-environment 'utf-8)
(set-terminal-coding-system 'utf-8)
(setq locale-coding-system 'utf-8)
(set-default-coding-systems 'utf-8)
(set-selection-coding-system 'utf-8)
(prefer-coding-system 'utf-8)
#+end_src

*** Bookmarks

Set bookmarks options and locations

#+begin_src emacs-lisp :tangle emacs.el
(setq bookmark-default-file (expand-file-name (concat config-dir "bookmarks")))
(setq bookmark-sort-flag nil)
(setq bookmark-alist '(("Home" (filename . "~/"))
                       ("Emacs folder" (filename . "~/work/src/emacs.d"))
                       ;; ("Qsdfgh home" (filename . "/scp:pierre@qsdfgh.com#38170:~/"))
                       ("Fotolia dev" (filename . "/scp:eqx-dev1:/home/plecocq/www/fotolia"))))
#+end_src

*** Display

Initialize display setup

#+begin_src emacs-lisp :tangle emacs.el
(toggle-frame-maximized)

(setq-default show-trailing-whitespace t
              highlight-tabs t
              mode-line-format
              (list
               '(:eval (if (buffer-modified-p)
                           (propertize "  %b" 'face 'bold-italic)
                         (propertize "  %b" 'face 'bold)))
               " (%l:%c) %p/%I - %m";; (format " %s" minor-mode-alist)
               '(which-function-mode (" " which-func-format))))

(when (member "Inconsolata" (font-family-list))
  (set-face-attribute 'default nil
                      :family "Inconsolata" ;; "DejaVu Sans Mono"
                      :height 120
                      :weight 'normal
                      :width 'normal))

(when (display-graphic-p)
  (setq show-paren-style 'expression
        select-enable-clipboard t)
  (set-fringe-mode 10))
#+end_src

*** File types

Associate files with major-modes. Or major-modes with files

#+begin_src emacs-lisp :tangle emacs.el
(add-to-list 'auto-mode-alist '("\\.log\\'"         . auto-revert-mode))
(add-to-list 'auto-mode-alist '("\\.js[on]\\'"      . js2-mode))
(add-to-list 'auto-mode-alist '("\\.asd\\'"         . lisp-mode))
(add-to-list 'auto-mode-alist '("\\.cl\\'"          . lisp-mode))
(add-to-list 'auto-mode-alist '("\\.lisp\\'"        . lisp-mode))
(add-to-list 'auto-mode-alist '("/tmp/mutt.*\\'"    . mail-mode))
(add-to-list 'auto-mode-alist '("\\.php-dev\\'"     . php-mode))
(add-to-list 'auto-mode-alist '("Dockerfile"        . ruby-mode))
(add-to-list 'auto-mode-alist '("Vagrantfile"       . ruby-mode))
(add-to-list 'auto-mode-alist '("Gemfile"           . ruby-mode))
(add-to-list 'auto-mode-alist '("Puppetfile"        . ruby-mode))
(add-to-list 'auto-mode-alist '("Rakefile"          . ruby-mode))
(add-to-list 'auto-mode-alist '("\\.rake\\'"        . ruby-mode))
(add-to-list 'auto-mode-alist '("\\.ru\\'"          . ruby-mode))
(add-to-list 'auto-mode-alist '("\\.gemspec\\'"     . ruby-mode))
(add-to-list 'auto-mode-alist '(".bashrc"           . shell-script-mode))
(add-to-list 'auto-mode-alist '(".zshrc"            . shell-script-mode))
(add-to-list 'auto-mode-alist '(".gnus"             . emacs-lisp-mode))
(add-to-list 'auto-mode-alist '("\\.html?\\'"       . web-mode))
(add-to-list 'auto-mode-alist '("\\.erb\\'"         . web-mode))
(add-to-list 'auto-mode-alist '("\\.erubis\\'"      . web-mode))
(add-to-list 'auto-mode-alist '("\\.ya?ml\\'"       . yaml-mode))
#+end_src

*** Auto-insert

Configure auto-insert-mode

#+begin_src emacs-lisp :tangle emacs.el
(auto-insert)

(setq auto-insert-alist
      '(((ruby-mode . "Ruby program") nil
         "#!/usr/bin/env ruby\n\n"
         "# File: " (file-name-nondirectory buffer-file-name) "\n"
         "# Time-stamp: <>\n"
         "# Copyright (C) " (substring (current-time-string) -4) " " (user-full-name) "\n"
         "# Description: " _ "\n\n")
        ((emacs-lisp-mode . "Emacs lisp mode") nil
         ";;; " (file-name-nondirectory buffer-file-name) " --- " _ "\n\n"
         ";; Time-stamp: <>\n"
         ";; Copyright (C) " (substring (current-time-string) -4) " " (user-full-name) "\n\n"
         ";;; Commentary:\n\n"
         ";;; Code:\n\n"
         ";;; " (file-name-nondirectory buffer-file-name) " ends here\n")
        ((c-mode . "C program") nil
         "/*\n"
         " * File: " (file-name-nondirectory buffer-file-name) "\n"
         " * Time-stamp: <>\n"
         " * Copyright (C) " (substring (current-time-string) -4) " " (user-full-name) "\n"
         " * Description: " _ "\n"
         " */\n\n")
        ((shell-mode . "Shell script") nil
         "#!/bin/bash\n\n"
         " # File: " (file-name-nondirectory buffer-file-name) "\n"
         " # Time-stamp: <>\n"
         " # Copyright (C) " (substring (current-time-string) -4) " " (user-full-name) "\n"
         " # Description: " _ "\n\n")))
#+end_src

* Packages

** Package manager

Initialize the internal package manager and install =use-package=

#+begin_src emacs-lisp :tangle emacs.el
(require 'package)

(setq package-user-dir packages-dir)

(setq package-archives
      '(("melpa"        . "http://melpa.org/packages/")
        ("gnu"          . "http://elpa.gnu.org/packages/")
        ("marmalade"    . "http://marmalade-repo.org/packages/")
        ("org"          . "http://orgmode.org/elpa/")))

(package-initialize)

(when (not package-archive-contents)
  (package-refresh-contents))

(setq package-enable-at-startup nil)

(unless (package-installed-p 'use-package)
  (package-install 'use-package))

(require 'use-package)
#+end_src

** External packages

Install external packages from Emacs repositories

*** Anzu

A minor mode which displays current match and total matches information ([[https://github.com/syohex/emacs-anzu][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package anzu
             :ensure t
             :init (progn
                     (global-anzu-mode +1)
                     (set-face-attribute 'anzu-mode-line nil :foreground "yellow")))
#+end_src

*** Autopair

Automagically pair braces and quotes in emacs ([[https://github.com/capitaomorte/autopair][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package autopair
             :ensure t
             :init (autopair-global-mode t))
#+end_src

*** Bonjourmadame

Say “Hello ma'am!” ([[https://github.com/pierre-lecocq/bonjourmadame][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package bonjourmadame
             :ensure t)
#+end_src

*** Browse-kill-ring

For when 'C-y M-y M-y M-y' gets you down ([[https://github.com/browse-kill-ring/browse-kill-ring][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package browse-kill-ring
             :ensure t)
#+end_src

*** Company

Modular in-buffer completion framework for Emacs ([[https://github.com/company-mode/company-mode][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package company
             :ensure t
             :init (progn
                     (setq company-auto-complete nil
                           company-tooltip-flip-when-above t
                           company-minimum-prefix-length 2
                           company-tooltip-limit 10
                           company-idle-delay 0.5)
                     (global-company-mode 1)))
#+end_src

*** Darkmine-theme

Yet another emacs dark color theme ([[https://github.com/pierre-lecocq/darkmine-theme][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package darkmine-theme
             :ensure t
             :init (load-theme 'darkmine t))
#+end_src

*** HTMLize

Convert buffer text and decorations to HTML ([[http://emacswiki.org/emacs/Htmlize][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package htmlize
             :ensure t)
#+end_src

*** Ido

Interactively do things ([[http://emacswiki.org/emacs/InteractivelyDoThings][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package flx-ido
             :ensure t)

(use-package ido-hacks
             :ensure t)

(use-package ido-vertical-mode
             :ensure t)

(use-package ido
             :ensure t
             :init (progn
                     (require 'ido)
                     (require 'ido-hacks)
                     (setq ido-case-fold t
                           ido-enable-flex-matching t
                           ido-use-filename-at-point 'guess
                           ido-create-new-buffer 'always
                           ido-use-virtual-buffers t)
                     (ido-everywhere 1)
                     (flx-ido-mode 1)
                     (ido-mode t)
                     (ido-hacks-mode)
                     (ido-vertical-mode)))
#+end_src

*** Idle-highlight-mode

Highlight word at point on idle ([[https://github.com/nonsequitur/idle-highlight-mode][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package idle-highlight-mode
             :ensure t)
#+end_src

*** JS2-mode

Improved JavaScript editing mode ([[https://github.com/mooz/js2-mode][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package js2-mode
             :ensure t)
#+end_src

*** Markdown-mode

Emacs Major mode for Markdown-formatted text files ([[http://jblevins.org/projects/markdown-mode/][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package markdown-mode
             :ensure t)
#+end_src

*** Org-mode

Org mode is for keeping notes, maintaining TODO lists, planning projects, and authoring documents with a fast and effective plain-text system ([[http://orgmode.org/][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package org
  :ensure t
  :init (progn
          (setq org-directory (expand-file-name "~/org-files/"))
          (setq org-hide-leading-stars t
                org-hide-emphasis-markers t
                org-fontify-done-headline t
                org-src-fontify-natively t
                org-default-notes-file (expand-file-name (concat org-directory "notes.org"))
                org-agenda-files (expand-file-name (concat org-directory "agenda.org")))))
#+end_src

*** PHP-mode

A PHP mode for GNU Emacs ([[https://github.com/ejmr/php-mode][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package php-mode
             :ensure t)
#+end_src

*** Rainbow-delimiters-mode

Emacs rainbow delimiters mode ([[https://github.com/Fanael/rainbow-delimiters][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package rainbow-delimiters
             :ensure t)
#+end_src

*** Rainbow-mode

Colorize color strings ([[https://julien.danjou.info/projects/emacs-packages][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package rainbow-mode
             :ensure t)
#+end_src

*** Ruby-mode

Font-locking, indentation support, and navigation for Ruby code ([[http://emacswiki.org/emacs/RubyMode][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package ruby-mode
             :ensure t)
#+end_src

*** Slime

The Superior Lisp Interaction Mode for Emacs ([[https://common-lisp.net/project/slime/][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package slime-company
             :ensure t)

(use-package slime
             :ensure t
             :init (progn

                     (if (eq system-type 'darwin)
                         (setq inferior-lisp-program "/usr/local/bin/sbcl")
                       (setq inferior-lisp-program "sbcl"))
                     (slime-setup '(slime-company))))
#+end_src

*** Symon

Tiny graphical system monitor ([[https://github.com/zk-phi/symon/][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package symon
             :ensure t
             :init (progn
                     (setq symon-delay 5)
                     (symon-mode t)))
#+end_src

*** Web-mode

Web template editing mode for emacs ([[http://web-mode.org/][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package web-mode
             :ensure t)
#+end_src

*** Whitespace

A minor mode to visualize blanks ([[http://emacswiki.org/emacs/WhiteSpace][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package whitespace
             :ensure t
             :init (progn
                     (setq whitespace-line-column 80
                           whitespace-style '(tabs tab-mark face)
                           whitespace-global-modes '(not org-mode web-mode))
                     (global-whitespace-mode)))
#+end_src

*** Yaml-mode

The emacs major mode for editing files in the YAML data serialization format ([[https://github.com/yoshiki/yaml-mode][source]])

#+begin_src emacs-lisp :tangle emacs.el
(use-package yaml-mode
             :ensure t)
#+end_src

* Hooks

** Minibuffer hook

#+begin_src emacs-lisp :tangle emacs.el
(defun hook-minibuffer-setup ()
  "Hook for Minibuffer setup."
  (setq show-trailing-whitespace nil))

(add-hook 'minibuffer-setup-hook #'hook-minibuffer-setup)
#+end_src

** Shell mode hook

#+begin_src emacs-lisp :tangle emacs.el
(defun hook-shell-mode ()
  "Hook for Shell mode."
  (setq show-trailing-whitespace nil)
  (eshell/alias "l" "ls -l")
  (eshell/alias "la" "ls -la"))

(add-hook 'shell-mode-hook #'hook-shell-mode)
(add-hook 'eshell-mode-hook #'hook-shell-mode)
#+end_src

** Dired mode hook

#+begin_src emacs-lisp :tangle emacs.el
(defun hook-dired-mode ()
  "Hook for Dired mode."
  (put 'dired-find-alternate-file 'disabled nil))

(add-hook 'dired-mode-hook #'hook-dired-mode)
#+end_src

** Text mode hook

#+begin_src emacs-lisp :tangle emacs.el
(defun hook-text-mode ()
  "Hook  for Text mode."
  (linum-mode 1)
  (make-local-variable 'linum-format)
  (setq linum-format " %d "))

(add-hook 'text-mode-hook #'hook-text-mode)
#+end_src

** Prog mode hook

#+begin_src emacs-lisp :tangle emacs.el
(defun hook-prog-mode ()
  "Hook for Prog mode."
  (idle-highlight-mode t)
  (local-set-key (kbd "C-c <right>") 'hs-show-block)
  (local-set-key (kbd "C-c <left>")  'hs-hide-block)
  (local-set-key (kbd "C-c <up>")    'hs-hide-all)
  (local-set-key (kbd "C-c <down>")  'hs-show-all)
  (hs-minor-mode t)
  (rainbow-delimiters-mode)
  (rainbow-mode))

(add-hook 'prog-mode-hook #'hook-prog-mode)
#+end_src

** C mode hook

#+begin_src emacs-lisp :tangle emacs.el
(defun hook-c-mode ()
  "Hook for C mode."
  (c-set-offset 'case-label '+))

(add-hook 'c-mode-common-hook #'hook-c-mode)
#+end_src

** PHP mode hook

#+begin_src emacs-lisp :tangle emacs.el
(defun hook-php-mode ()
  "Hook for PHP mode."
;;  (require 'php-extras)
  (setq comment-start "// "
        comment-end "")
  (set (make-local-variable 'indent-tabs-mode) nil))

(add-hook 'php-mode-hook #'hook-php-mode)
#+end_src

** Emacs lisp mode hook

#+begin_src emacs-lisp :tangle emacs.el
(defun hook-emacs-lisp-mode ()
  "Hook for Emacs Lisp mode."
  (eldoc-mode))

(add-hook 'emacs-lisp-mode-hook #'hook-emacs-lisp-mode)
#+end_src

** Makefile mode hook

#+begin_src emacs-lisp :tangle emacs.el
(defun hook-makefile-mode ()
  "Hook for Makefile mode."
  (whitespace-toggle-options '(tabs))
  (setq indent-tabs-mode t))

(add-hook 'makefile-mode-hook #'hook-makefile-mode)
#+end_src

** Before save hook

#+begin_src emacs-lisp :tangle emacs.el
(defun hook-before-save ()
  "Hook before save."
  (time-stamp)
  (delete-trailing-whitespace)
  (whitespace-cleanup))

(add-hook 'before-save-hook #'hook-before-save)
#+end_src

** After save hook

#+begin_src emacs-lisp :tangle emacs.el
(defun hook-after-save ()
  "Hook after save."
  (when (file-exists-p (byte-compile-dest-file buffer-file-name))
    (emacs-lisp-byte-compile)))

;; (add-hook 'after-save-hook #'hook-after-save)
#+end_src

* Functions

Some useful functions

** Toggle a shell buffer

Get an eshell buffer.
If it has already been launched, just get it back.
If it is the current one, switch to the previous buffer.

#+begin_src emacs-lisp :tangle emacs.el
(defun pl-get-shell ()
  "Get a shell buffer."
  (interactive)
  (if (eq (current-buffer) (get-buffer "*eshell*"))
      (switch-to-buffer (other-buffer (current-buffer) t))
    (progn
      (if (member (get-buffer "*eshell*") (buffer-list))
          (switch-to-buffer "*eshell*")
        (eshell)))))
#+end_src

** Kill buffers by mode

Kill all buffers that belongs to a given mode

#+begin_src emacs-lisp :tangle emacs.el
(defun pl-kill-buffers-by-mode (&optional mode-name)
  "Kill buffers by mode.  Ask which mode if MODE-NAME is not provided."
  (interactive)
  (unless mode-name
    (setq mode-name (read-from-minibuffer "Mode to kill: ")))
  (let ((killed-buffers 0)
        (mode-to-kill (intern mode-name)))
    (dolist (buffer (buffer-list))
      (when (eq mode-to-kill (buffer-local-value 'major-mode buffer))
        (setq killed-buffers (1+ killed-buffers))
        (kill-buffer buffer)))
    (message "%d buffer(s) killed" killed-buffers)))
#+end_src

** Force eval buffer

Force a buffer evaluation

#+begin_src emacs-lisp :tangle emacs.el
(defun pl-force-eval ()
  "Forced Emacs Lisp buffer evaluation - stolen from SO."
  (interactive)
  (save-excursion
    (goto-char (point-min))
    (while (not (eobp))
      (forward-sexp)
      (eval-defun nil))))
#+end_src

** Set frame transparency

Adjust transparency of the current frame

#+begin_src emacs-lisp :tangle emacs.el
(defun pl-transparency (value)
  "Set the transparency of the frame window.
Argument VALUE 0 = transparent, 100 = opaque."
  (interactive "nTransparency Value 0 - 100 opaque: ")
  (when (display-graphic-p)
    (set-frame-parameter (selected-frame) 'alpha value)))
#+end_src

** Fix org-mode font lock

Quick fix for fontification in org-mode

#+begin_src emacs-lisp :tangle emacs.el
(defun org-font-lock-ensure (beg end)
  "Org font lock ensure from BEG to END."
  (font-lock-ensure))
#+end_src

* Keybindings

All the keybindings

#+begin_src emacs-lisp :tangle emacs.el
(when (eq system-type 'darwin)
  (setq mac-option-modifier nil
        mac-command-modifier 'meta
        select-enable-clipboard t))

(global-set-key [delete] 'delete-char)

(global-set-key (kbd "M-g") 'goto-line)
(global-set-key (kbd "C-;") 'other-window)
(global-set-key (kbd "M-y") 'browse-kill-ring)
(global-set-key (kbd "M-o") 'occur)
(global-set-key (kbd "C-c C-c") 'comment-region)
(global-set-key (kbd "C-c C-u") 'uncomment-region)
(global-set-key (kbd "C-S-s") 'find-grep)
(global-set-key (kbd "C-S-f") 'imenu)

(global-set-key [f5] 'bookmark-bmenu-list)
(global-set-key [f6] 'recentf-open-files)
(global-set-key [f12] 'pl-get-shell)

(when (display-graphic-p)
  (global-unset-key (kbd "C-z")))

;; Credits to emacsfodder

(define-key occur-mode-map (kbd "<down>")
  (lambda ()
    (interactive)
    (occur-next)
    (occur-mode-goto-occurrence-other-window)
    (recenter)
    (other-window 1)))

(define-key occur-mode-map (kbd "<up>")
  (lambda ()
    (interactive)
    (occur-prev)
    (occur-mode-goto-occurrence-other-window)
    (recenter)
    (other-window 1)))
#+end_src

* Bootstrap

Load external files

#+begin_src emacs-lisp :tangle emacs.el
(dolist (f (list host-file))
  (when (file-exists-p f)
    (load f 'noerror)))
#+end_src

Tell me the time spent to load the config

#+begin_src emacs-lisp :tangle emacs.el
(message "Config successfully loaded in %s" (emacs-init-time))
#+end_src
